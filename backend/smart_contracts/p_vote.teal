#pragma version 2
// check if the app is being created
// if so save creator
int 0
txn ApplicationID // check if the ApplicationID exists (if doesn't exist it equals zero)
==
bz not_creation // branch to target if ApplicationID is zero (bz -> branch if zero)
byte "Creator"
txn Sender // Sender address stored as "Creator" variable
app_global_put // write previous valued to global storage (it's like a dictionary where "Creator" is the key, and Sender is the value)
//4 args must be used on creation
txn NumAppArgs
int 4
==
bz failed // check if number of app args is 4 and branch if they are not equal
//set round ranges
byte "RegistrationBegin"
txna ApplicationArgs 0
btoi // converts big endian bytes to uint64
app_global_put // put the first application arguement into global storage with "RegistrationBegin" key
byte "RegistrationEnd"
txna ApplicationArgs 1
btoi
app_global_put // put the 2nd application arguement into global storage with "RegistrationEnd" key
byte "VoteBegin"
txna ApplicationArgs 2
btoi
app_global_put
byte "VoteEnd"
txna ApplicationArgs 3
btoi
app_global_put
int 1
return // Returns the value on the top of the stack when the program terminates, for successful creation of smart contract, return 1
not_creation: // branch for any call that is not contract creation
// check if this is deletion ---
int DeleteApplication
txn OnCompletion
==
bz not_deletion // branch if not equal
byte "Creator"
app_global_get // Get the "Creator" byte from global storage
txn Sender
== // Check if the "Creator" is equal to the sender of the current transaction
bz failed // Fail if "Creator" is not sender
int 1
return // Return 1 to complete app deletion
not_deletion:
//---
// check if this is update ---
int UpdateApplication
txn OnCompletion
==
bz not_update
byte "Creator"
app_global_get
txn Sender
==
bz failed
int 1
return
not_update:
//---
// check for closeout
int CloseOut
txn OnCompletion
== // branch if equal (not zero) -- bnz -> branch if not zero
bnz close_out
// register
txna ApplicationArgs 0
byte "register" // check if the first argument to the transaction is "register"
==
bnz register
//vote
txna ApplicationArgs 0
byte "vote" // check if the first argument to the transaction is "vote"
==
bnz vote
int 0 // handle case where arguement is either not "register" or not "vote"
return
vote:
//verify in voting rounds
global LatestTimestamp
byte "VoteBegin"
app_global_get // check that current Round is after votebegin
>=
global LatestTimestamp
byte "VoteEnd"
app_global_get // and check that current Round is before voteend
<=
&&
bz failed // if one of the above conditions is false, branch to failed
// Check that the account has opted in
// account offset (0 == sender, 
// 1 == txn.accounts[0], 2 == txn.accounts[1], etc..)
int 0 // offset of 0 corresponds to sender
txn ApplicationID // B -> AppId
app_opted_in // check if account A (0 -> sender) opted in to application B (specified by AppId)
bz failed
// check if they have the vote token
// assuming assetid 2. This should
// be changed to appropriate asset id
// sender
int 0
// hard-coded assetid
int 2
// returns frozen an asset balance
// pop frozen
asset_holding_get AssetBalance
pop
// does voter have at least 1 vote token
int 1
>=
bz failed
// two transactions
global GroupSize
int 2
==
bz failed
// second tx is an asset xfer
gtxn 1 TypeEnum
int 4
==
bz failed
// creator receiving the vote token
byte "Creator"
app_global_get
gtxn 1 AssetReceiver
==
bz failed
// verify the proper token is being spent
gtxn 1 XferAsset
// hard coded and should be changed
int 2
==
bz failed
// spent 1 vote token
gtxn 1 AssetAmount
//
int 1
==
bz failed
//check local to see if they have voted
int 0 //sender
txn ApplicationID
byte "voted"
app_local_get_ex 
// if voted skip incrementing count
bnz voted
pop
// can only vote for candidatea
// or candidateb
txna ApplicationArgs 1
byte "candidatea" 
==
txna ApplicationArgs 1
byte "candidateb" 
==
||
bz failed
// read existing vote candiate
// in global state and increment vote
int 0
txna ApplicationArgs 1
app_global_get_ex
bnz increment_existing
pop
int 0
increment_existing:
int 1
+
store 1
txna ApplicationArgs 1
load 1
app_global_put
// store the voters choice in local state
int 0 //sender
byte "voted"
txna ApplicationArgs 1
app_local_put
int 1
return
voted:
pop
int 1
return
register:
// global LatestTimestamp // No minimum registration time
// byte "RegistrationBegin"
// app_global_get
// >=
global LatestTimestamp
byte "RegistrationEnd"
app_global_get
<=
// &&
int OptIn
txn OnCompletion
==
&&
bz failed
int 1
return
//call if this is a closeout op
close_out:
int 1
return
failed:
int 0
return
finished:
int 1
return