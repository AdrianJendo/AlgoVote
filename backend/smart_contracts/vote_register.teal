#pragma version 5

// check if application exists
int 0
txn ApplicationID
==
bz not_creation

// init loop var (0 for first argument)
int 0
loop:
dup // txnas pops top of stack so duplicate the value
txnas ApplicationArgs // get the address using top of stack value
int 0 // set registered value to 0 (false)
app_global_put // store 0 in global state
int 1 // increment counter by 1 and check upper bound
+
dup // < operator pops off stack so duplicate the value for next iteration
txn NumAppArgs // loop over all candidates
<
bnz loop
b success

not_creation:
// check if regular txn
txn OnCompletion
int NoOp
==
bnz handle_noop

// check deletion
txn OnCompletion
int DeleteApplication
==
bnz handle_deleteapp

// check opt in (no local state so no opting in)
txn OnCompletion
int OptIn
==
bnz failed

// check close out (clears local state -> we don't want that)
txn OnCompletion
int CloseOut
==
bnz failed

// check update application (don't want to update anything, fail this too)
txn OnCompletion
int UpdateApplication
==
bnz failed

// Unexpected OnCompletion value. Should be unreachable.
b failed

// Handle NoOp
handle_noop:
b success

handle_deleteapp:
global CreatorAddress
txn Sender
==
bz failed
b success


failed:
int 0
return
success:
int 1
return